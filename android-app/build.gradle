apply plugin: 'android'

repositories {
    maven {
        url 'https://github.com/AfzalivE/my-mvn-repo/raw/master/'
    }
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:13.0.+'
    compile "com.actionbarsherlock:actionbarsherlock:4.3.2-SNAPSHOT"
    compile "com.jeremyfeinstein:slidingmenu:1.3-SNAPSHOT"
    compile "de.keyboardsurfer:crouton:1.8.1"
    compile "com.koushikdutta.ion:ion:1.1.6-SNAPSHOT"

    compile files('libs/commons-lang3-3.1.jar')
    compile files('libs/kefirbb-1.0.jar')
    compile files('libs/libGoogleAnalyticsV2.jar')
}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))

    defaultConfig {
        versionCode versionProps['code'].toInteger()
        versionName versionProps['name']
    }

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            assert localProps['keystore.props.file']
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

            storeFile file(keyProps['store'])
            keyAlias keyProps['alias']
            storePassword keyProps['storePass']
            keyPassword keyProps['pass']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}
