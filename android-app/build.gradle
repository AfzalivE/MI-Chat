task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

apply plugin: 'android'

repositories {
    maven {
        url 'https://github.com/AfzalivE/my-mvn-repo/raw/master/'
    }
    mavenCentral()
}

dependencies {
    compile "com.android.support:support-v4:18.0.+"
    compile "com.android.support:appcompat-v7:18.0.+"
    compile "com.jeremyfeinstein:slidingmenu:1.3-SNAPSHOT"
    compile "com.loopj:android-async-http:1.4.3"
    compile "de.keyboardsurfer:crouton:1.8.1"
    compile "uk.co.senab:photoview:1.2.1"
    compile "com.koushikdutta.urlimageviewhelper:urlimageviewhelper:1.0.4"
    compile "org.sufficientlysecure:donations:2.0.0"

    compile fileTree(dir: 'libs', include: '*.jar')
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.0.1"

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))

    defaultConfig {
        versionCode versionProps['code'].toInteger()
        versionName versionProps['name']
    }

    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            assert localProps['keystore.props.file']
            keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

            storeFile file(keyProps['store'])
            keyAlias keyProps['alias']
            storePassword keyProps['storePass']
            keyPassword keyProps['pass']
        }
    }

    buildTypes {
        debug {
//            buildConfig "public final static String BASE_URI = \"http://www.macinsiders.com/chat/?ajax=true&channelID=500006296\";"
            buildConfig "public final static String BASE_URI = \"http://www.macinsiders.com/chat/?ajax=true\";"
        }
        release {
            buildConfig "public final static String BASE_URI = \"http://www.macinsiders.com/chat/?ajax=true\";"
            signingConfig signingConfigs.release
        }
    }
}
